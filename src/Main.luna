
import Std.Base
import Std.Foreign
import Std.Foreign.C.Value

def log text:
    if False then print text else None

def bsonc symbol:
    log ("Will lookup " + symbol)
    lookupSymbol "bson-1.0" symbol

def mongoc symbol: 
    log ("Will lookup " + symbol)
    lookupSymbol "libmongoc-1.0" symbol

def mongoh symbol: 
    log ("Will lookup " + symbol)
    lookupSymbol "MongoHelper" symbol

def bson_free ptr: 
    funptr = bsonc "bson_free"
    funptr.call None [ptr.toCArg]

def bracket before after item:
    sth = before
    result = runError (item sth)
    after sth
    case result of
        Left error: throw error
        Right value: value

# :: Text -> (CArg -> a) -> a
def withStringCArg stringArgument method:
    bracket (CString . fromText stringArgument) (_.free) (method _.toCArg)

def singleStringMethod ptr ret method stringArgument:
    withStringCArg stringArgument strArg: 
        method.call ret [ptr.toCArg, strArg]

def twoStringMethod ptr ret method stringArgument stringArgument2:
    withStringCArg stringArgument strArg:
        withStringCArg stringArgument2 strArg2:
            method.call ret [ptr.toCArg, strArg, strArg2]

def readTextUnownedFromCStr ptr:
    (CString.fromRetType ptr).toText

# Name is slightly misleading - bson refers just to the fact that memory for this string is managed by bsonc
def consumeBsonStringToText ptr:
    result_cstr = CString.fromRetType ptr
    text = result_cstr.toText
    bson_free ptr
    text

class Collection:
    ptr :: ManagedPointer None

    def insertOne documentJsonText:
        result = singleStringMethod self.ptr (Pointer CChar) (mongoh "mongoh_insert_one") documentJsonText
        case result.isNull of 
            True: reportError
            False: 
                print (consumeBsonStringToText result.ptr)

class Database:
    ptr :: ManagedPointer None
    def get_collection_names:
        bson_strfreev = bsonc "bson_strfreev"
        mongoh_get_collection_names = mongoh "mongoh_get_collection_names"
        result = mongoh_get_collection_names.call (Pointer (Pointer CChar)) [self.ptr.toCArg]
        case result.isNull of 
            True: 
                reportError
                []
            False: 
                nta = NullTerminatedArrayVal result
                listOfNames = nta.toList.each (ptr: (CString.fromRetType ptr.ptr).toText)
                bson_strfreev.call None [result.toCArg]
                listOfNames

class Client:
    ptr :: ManagedPointer None


    def get_collection dbname collectionName:
        mongoc_collection_destroy = mongoc "mongoc_collection_destroy"
        collectionHandle = twoStringMethod self.ptr (Pointer None) (mongoc "mongoc_client_get_collection") dbname collectionName
        managedCollectionHandle = ManagedPointer None . fromPointer mongoc_collection_destroy collectionHandle
        Collection managedCollectionHandle

    def set_appname: 
        singleStringMethod self.ptr CInt8 (mongoc "mongoc_client_set_appname")

    def get_database dbname: 
        mongoc_database_destroy = mongoc "mongoc_database_destroy"
        dbHandle = singleStringMethod self.ptr (Pointer None) (mongoc "mongoc_client_get_database") dbname
        Database (ManagedPointer None . fromPointer mongoc_database_destroy dbHandle)

    def simple_command dbname commandJsonText:
        result = twoStringMethod self.ptr (Pointer CChar) (mongoh "simpleCommand") dbname commandJsonText
        case result.isNull of 
            True: reportError
            False: 
                print (consumeBsonStringToText result.ptr)

class MongoDB:
    def init:
        mongoc_init = mongoc "mongoc_init"
        mongoc_init.call None []
    def cleanup:
        mongoc_cleanup = mongoc "mongoc_cleanup"
        mongoc_cleanup.call None []

    def new_client uri:
        withStringCArg uri (uri_cstr):
            mongoc_client_new = mongoc "mongoc_client_new"
            mongoc_client_destroy = mongoc "mongoc_client_destroy"
            ptr = mongoc_client_new.call (Pointer None)  [uri_cstr]
            managedHandle = ManagedPointer None . fromPointer mongoc_client_destroy ptr
            Client managedHandle

def reportError:
    getLastError = mongoh "mongoh_get_error"
    error = getLastError.call (Pointer CChar) []
    case error.isNull of 
            True:  print "No error information"
            False: throw (CString.fromRetType error.ptr).toText

def main:
    MongoDB.init
    client = MongoDB.new_client "mongodb://192.168.11.20:27017"
    client.set_appname "lunawrapper"

    db = client.get_database "lunaDB"
    print db.get_collection_names

    client.simple_command "admin" '{"ping": 1}'
    print (runError (client.simple_command "admin" '{"pung": 1}'))
    collection = client.get_collection "lunaDB" "lunaCollection"
    collection.insertOne '{"hello": "world"}'
    print (runError (collection.insertOne '{"hello": world}'))


def test_bracket:
    def beforr:
        print "before"
        "50"
    def riskyOperation arg:
        print "doing risky thing"
        throw "ajajaj"
    bracket (beforr) (arg: print ("After " + arg)) riskyOperation
