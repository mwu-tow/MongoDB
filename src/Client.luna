import Std.Base
import Std.Foreign
import Std.Foreign.C.Value

import MongoDB.Collection
import MongoDB.Database
import MongoDB.Utils

class Client:
    ptr :: ManagedPointer None


    def collection dbname collectionName:
        mongoc_collection_destroy = mongoc "mongoc_collection_destroy"
        collectionHandle = twoStringMethod self.ptr (Pointer None) (mongoc "mongoc_client_get_collection") dbname collectionName
        managedCollectionHandle = managedPtr mongoc_collection_destroy collectionHandle
        Collection managedCollectionHandle

    def setAppname: 
        singleStringMethod self.ptr CInt8 (mongoc "mongoc_client_set_appname")

    def database dbname:
        dbHandle = singleStringMethod self.ptr (Pointer None) (mongoc "mongoc_client_get_database") dbname
        databaseFromPointer dbHandle

    def defaultDatabase: 
        mongoc_client_get_default_database = mongoc "mongoc_client_get_default_database"
        dbHandle = mongoc_client_get_default_database.call (Pointer None) [self.ptr.toCArg]
        if dbHandle.isNull then Nothing else Just (databaseFromPointer dbHandle)
    
    def simpleCommand dbname commandJson:
        result = twoStringMethod self.ptr (Pointer CChar) (mongoh "simpleCommand") dbname commandJson.toText
        case result.isNull of 
            True: reportError
            False: 
                print (consumeBsonStringToText result.ptr)